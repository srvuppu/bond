// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE file in the project root for full license information.

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.bondlib:bond-gradle:8.0.0-SNAPSHOT'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.9.1'
    id "net.linguica.maven-settings" version "0.5"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.bondlib.gradle'

group 'org.bondlib'
version '8.0.0-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'https://msazure.pkgs.visualstudio.com/_packaging/Bond%40Local/maven/v1'
        name 'Bond@Local'
        authentication {
            basic(BasicAuthentication)
        }
    }
}

dependencies {
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'com.github.chrisvest', name: 'stormpot', version: '2.4.2'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'

    testCompile 'junit:junit:4.12'
}

publishing {
    repositories {
        maven {
            url 'https://msazure.pkgs.visualstudio.com/_packaging/Bond/maven/v1'
            credentials {
                username "VSTS"
                password project.hasProperty('vstsMavenAccessToken') ? "${vstsMavenAccessToken}" : ''
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

// Some tests have non-ASCII strings that are UTF-8 encoded. In environments
// with system encodings other than UTF-8, javac will fail on those tests.
compileTestJava {
    options.encoding = 'UTF-8'
}

compileBond {
    bondfiles '../../idl/bond/core/bond.bond',
            '../../idl/bond/core/bond_const.bond'
    options '-n', 'bond=org.bondlib'
}

compileTestBond {
    options '--import-dir', 'src/test/bond/imports/'
}
